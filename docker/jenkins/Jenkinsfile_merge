/*****************************************************************************
 * Copyright (C) 2021, RTE (http://www.rte-france.com)
 * SPDX-License-Identifier: CC-BY-4.0
 *
 * Jenkins pipeline triggered from job 'ci' in order to merge GitLab's
 * sfl/master into rte/master.
 *
 * Requirements:
 * - SSH Agent Plugin
 * - Set SSH key with name 'gitlab-credentials'
 * - Set properties file (by default '/var/jenkins_home/jenkins.properties')
 *****************************************************************************/

def p // Global variable to handle properties

def project_list = ["meta-seapath",
                    "yocto-bsp",
                    "repo-manifest",
                    "ansible",
                    "vm_manager",
                    "votp-inventories"]

pipeline {
    agent any

    // TODO: Add gerrit and gitlab server fingerprints to known_hosts instead
    environment {
        GIT_SSH_COMMAND = "ssh -o StrictHostKeyChecking=no"
        CONFIG_FILE = "/var/jenkins_home/jenkins.properties"
    }

    stages {

        stage("Prepare workspace") {
            steps {
                cleanWs()
                script {
                    if (!fileExists("${CONFIG_FILE}")) {
                        error "Could not find configuration properties file: ${CONFIG_FILE}"
                    }
                    p = readProperties file: "${CONFIG_FILE}"
                }
            }
        }

        stage('Build') {
            steps {
                sshagent(credentials : ['gitlab-credentials']) {
                    script {
                        for (project in project_list) {
                            stage("Merge ${project}") {
                                sh """
                                    git clone -b rte/master \
                                    "[git@$p.GITLAB_URL]:$p.GITLAB_USER/${project}.git"
                                    cd ${project}
                                    git merge --ff-only origin/sfl/master
                                    git push origin rte/master
                                """
                            }
                        }
                    }
                }
            }
        }
    }

    post {
        success {
            echo 'Succeed'
        }

        failure {
            echo "Did not succeed!"
        }
    }
}
